; Implements the TI HDQ protocol used by battery "gas gauge" ICs
; Acts as a slave device, responding to commands from a master

; Must run at 1/8 MHz, 1 clock cycle = 8 us
; Auto-push should happen at 9 bits
; 
; IN FIFO format: 0000 0000 1000 0000 0000 0000 0000 0000
;                 |         |
;             byte read    valid bit
; if valid bit is not set, discard byte and treat as BREAK

.program hdq_slave
; side set is just for debugging
.side_set 1 opt
.wrap_target

read_byte:
    set pindirs, 0 side 0
    set x, 7

    set y, 1
    in y, 1 ; shift valid bit in

read_bit:
    wait 0 pin 0 [1]
    jmp pin read_bit ; stray edge, ignore it
    nop [5]
    in pins, 1 [7]
    nop [2] ; because above cannot be 10
    jmp pin notbreak 
    ; break condition
    push side 0 ; push the incomplete byte so the valid bit is NOT set
    wait 1 pin 0
    jmp read_byte
notbreak:
    jmp x-- read_bit

    ;pull

; fall through from reading to try writing
write_byte:
    pull
    out x, 1 side 1
    jmp !x read_byte ; if x is 0, the rest is garbage
    set x, 7 ; write 8 bits
    set pindirs, 1

write_bit:
    nop ; drive high for 200 us, to satisfy minimum time between falling edges cheaply
    nop [7] ; because above cannot be 24
    nop [7]
    nop [7]
    out y, 1 ; shift next bit into y
    set pins 0 [4] ; drive low for minimum of 25 us
    jmp y-- one_skip ; 1 skips extra nops, 0 falls through
    nop [7] ; 7 + 1 + 5 * 8 = 104 us
one_skip:
    set pins 1
    jmp x-- write_bit

.wrap

% c-sdk {
//; this is a raw helper function for use by the user which sets up the GPIO output, and configures the SM to output on a particular pin

#include "hardware/clocks.h"

void hdq_slave_program_init(PIO pio, uint sm, uint offset, uint pin) {
    pio_gpio_init(pio, pin);
    pio_sm_config c = hdq_slave_program_get_default_config(offset);

    sm_config_set_set_pins(&c, pin, 1);
    sm_config_set_in_pins (&c, pin);
    sm_config_set_jmp_pin (&c, pin);

    float div = clock_get_hz (clk_sys) * (1e-6 * 8); // 1/8 MHz
    sm_config_set_clkdiv (&c, div);

    sm_config_set_in_shift (&c, true, true, 9);

    // Output Shift Register configuration settings
    sm_config_set_out_shift (
        &c,
        true,           // shift direction: right
        false,           // autopull: enabled
        9   // autopull threshold
    );

    //; Sideset pin for debugging
    pio_gpio_init(pio, 13);
    pio_sm_set_consecutive_pindirs(pio, sm, 13, 1, true);
    sm_config_set_sideset_pins (&c, 13);

    pio_sm_init(pio, sm, offset, &c);
}
%}